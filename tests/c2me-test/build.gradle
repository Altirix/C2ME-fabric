import com.ishland.c2me.gradle.RedirectingOutputStream

evaluationDependsOn(":tests:test-mod")

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

loom {
    runs {
        testC2MEServer {
            server()
            runDir getRootProject().getProjectDir().toPath().relativize(project.getBuildDir().toPath()).resolve("testServer").toString()
            source sourceSets.testmod
            vmArgs "-Dcom.ishland.c2me.common.config.threadedWorldGen.allowThreadedFeatures=true",
                    "-Dcom.ishland.c2me.common.config.threadedWorldGen.useGlobalBiomeCache=true",
                    "-Dcom.ishland.c2me.common.config.threadedWorldGen.reduceLockRadius=true"
        }
    }
}

runTestC2MEServer.configure {
    doFirst {
        standardOutput = new RedirectingOutputStream(project, getName())
    }
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    testmodImplementation project(":tests:test-mod").sourceSets.main.output
    testmodImplementation project(":").sourceSets.main.output
    testmodImplementation project(":").configurations.getByName("shadowImpl").allDependencies
    modApi 'com.github.ishland:Starlight:08d63f4095'
}

test {
    useJUnitPlatform()
}